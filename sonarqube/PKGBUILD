#Maintaner: Yahya Mohajer <yaya_2013 {AT} yahoo {DOT} com >
#Edited by: Bernhard Friedreich <friesoft@gmail.com>

pkgname=sonarqube
pkgver=4.1.2
pkgrel=1
pkgdesc="A code quality management platform."
url="http://sonar.codehaus.org"
arch=('i686' 'x86_64')
license=('GPL3')
depends=('java-environment')
optdepends=('apache: a full featured webserver'
            'mysql: A fast SQL database server'
            'maven: a java project management and project comprehension tool')

backup=('opt/sonarqube/conf/sonar.properties' 
        'opt/sonarqube/conf/wrapper.conf'
        'opt/sonarqube/conf/logback.xml')

install=${pkgname}.install
conflicts=('java-sonar' 'sonar')
provides=('java-sonar' 'sonar')
replaces=('sonar')
options=(!strip)

source=(http://dist.sonar.codehaus.org/${pkgname}-${pkgver}.zip
        'sonarqube.sh'
        'wrapper.conf'
        'logback.xml'
	'sonarqube.service')	

md5sums=('2a753b00dacbf37450e80fd1aad50a9d'
         '2c806e51d269b746a4a2bd68b1083fbf'
         '9110665deb59e2b3ae7d1fc15a175a00'
         'c011c0403e396678351250e1ecacc6ee'
         '5a9c856eac5eb4f7bbad2495970758d9')

package() {
  cd ${srcdir}

  # Create directory and copy everything
  install -d ${pkgdir}/opt/${pkgname}

  # moving only $CARCH specific files to pkg, delete the rest
  msg "Determine right architecture"
  if [ $CARCH = 'x86_64' ]; then
    cp -r ${srcdir}/${pkgname}-${pkgver}/bin/linux-x86-64 ${pkgdir}/opt/${pkgname}/bin || return 1
    rm -r ${srcdir}/${pkgname}-${pkgver}/bin || return 1
  elif [ $CARCH = 'i686' ]; then
    cp -r ${srcdir}/${pkgname}-${pkgver}/bin/linux-x86-32 ${pkgdir}/opt/${pkgname}/bin || return 1
    rm -r ${srcdir}/${pkgname}-${pkgver}/bin || return 1
  fi

  # install the additional config files to the desired destination
  msg "Installing configuration files"
  mkdir -p ${pkgdir}/opt/${pkgname}/conf
  install ${srcdir}/${pkgname}-${pkgver}/conf/sonar.properties ${pkgdir}/opt/${pkgname}/conf/sonar.properties
  install ${srcdir}/logback.xml ${pkgdir}/opt/${pkgname}/conf/logback.xml
  install ${srcdir}/wrapper.conf ${pkgdir}/opt/${pkgname}/conf/wrapper.conf || return 1
  rm -r ${srcdir}/${pkgname}-${pkgver}/conf

  # copy documentation
  msg "Copy documentation"
  mkdir -p ${pkgdir}/usr/share/doc/${pkgname}/
  install ${srcdir}/${pkgname}-${pkgver}/COPYING ${pkgdir}/usr/share/doc/${pkgname}
  rm ${srcdir}/${pkgname}-${pkgver}/COPYING

  # delete not needed directories
  rm -r ${srcdir}/${pkgname}-${pkgver}/logs
  ln -s /var/log/${pkgname} ${pkgdir}/opt/sonarqube/logs

  # copy the source to the final directory
  msg "Copy Source to final directory"
  cp -a ${srcdir}/${pkgname}-${pkgver}/* ${pkgdir}/opt/${pkgname} || return 1

  install ${srcdir}/${pkgname}.sh ${pkgdir}/opt/${pkgname}/bin/sonar.sh || return 1

  mkdir -p ${pkgdir}/var/log/${pkgname}/
  touch ${pkgdir}/var/log/${pkgname}/wrapper.log
  touch ${pkgdir}/var/log/${pkgname}/sonar.log
  touch ${pkgdir}/var/log/${pkgname}/profiling.log

  install -m755 -d ${pkgdir}/opt/${pkgname}/run

  install -Dm644 "${srcdir}/sonarqube.service" "${pkgdir}/usr/lib/systemd/system/sonarqube.service"
}

