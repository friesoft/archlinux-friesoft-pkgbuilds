# $Id$
# Contributor: friesoft <friesoft@gmail.com>
# This package provides the 4.5 branch of Qt to be installed in parallel to existing Qt installations

pkgname=qt-45-git-parallel
pkgver=20091108
pkgrel=1
pkgdesc="The C++ Qt GUI framework - 4.5 series - for parallel installation"
arch=('i686' 'x86_64')
url="http://www.qtsoftware.com"
license=('GPL3' 'LGPL')

depends=('libpng' 'mesa' 'fontconfig' 'libtiff' 'libmng' 'sqlite3' 'libxrandr' 'glib2' 'libxi' 'dbus' 'libxcursor' 'libxinerama' 'libxrender' 'ca-certificates')
optdepends=('postgresql-libs' 'libmysqlclient' 'unixodbc-compat')                                    ## unixodbc-compat can also be replaced by either unixodbc or iodbc
makedepends=('inputproto' 'postgresql-libs' 'mysql' 'unixodbc-compat' 'cups' 'libxfixes' 'gtk2')
options=('!libtool' 'docs')

# desktop files and qdoc3 patch -- needed for qtdesigner-git build
source=('assistant.desktop' 'designer.desktop' 'linguist.desktop' 'qtconfig.desktop' 'qdoc3.patch')

md5sums=('a17befd344ff03baf2376447dde5710b'
         'e92f0f2cf7e2dbc33e8b150ca69f82c6'
         '2d83a66a41e4aae805c0ee41eb3b2ebf'
         '168ccdc967b3524f3fa3ce2b27e7b28e'
         '9c0a0db3569e889a54a6cddcd55c538c')

_gitroot="git://gitorious.org/qt/qt.git"
_gitname="qt"

build() {
          msg "Connecting to git server...."

          if [ -d ${srcdir}/$_gitname ] ; then
                    cd $_gitname
                    git pull git://gitorious.org/qt/qt.git 4.5

                    msg "The local files have been updated to the current revision"

                    cd $srcdir/$_gitname
          else
                    git clone $_gitroot

                    cd $srcdir/$_gitname

                    # branch: 4.5
                    git checkout --track -f -b 4.5 remotes/origin/4.5

                    # apply qdoc3 patch
                    patch -p0 -i $srcdir/qdoc3.patch

                    sed -i "s|-O2|$CXXFLAGS|" mkspecs/common/g++.conf
                    sed -i "/^QMAKE_RPATH/s| -Wl,-rpath,||g" mkspecs/common/g++.conf
          fi

          unset QMAKESPEC
          export QT4DIR=$srcdir/$_gitname
          export PATH=${QT4DIR}/bin:${PATH}
          export LD_LIBRARY_PATH=${QT4DIR}/lib:${LD_LIBRARY_PATH}

          ./configure -confirm-license -opensource \
                    -prefix /opt/qt-45 \
                    -sysconfdir /etc \
                    -plugindir /opt/qt-45/lib/qt/plugins \
                    -translationdir /opt/qt-45/share/qt/translations \
                    -datadir /opt/qt-45/share/qt \
                    -docdir /opt/qt-45/share/doc/qt \
                    -examplesdir /opt/qt-45/share/doc/qt/examples \
                    -demosdir /opt/qt-45/share/doc/qt/demos \
                    -largefile \
                    -plugin-sql-{psql,mysql,sqlite,odbc} \
                    -system-sqlite \
                    -xmlpatterns \
                    -no-phonon \
                    -svg \
                    -webkit \
                    -scripttools \
                    -system-zlib \
                    -system-libtiff \
                    -system-libpng \
                    -system-libmng \
                    -system-libjpeg \
                    -openssl-linked \
                    -dbus-linked \
                    -reduce-relocations \
                    -no-separate-debug-info \
                    -gtkstyle \
                    -opengl \
                    -glib \
                    -cups \
                    -iconv \
                    -xinerama \
                    -xcursor \
                    -xfixes \
                    -qdbus

          # build qt 4.5 branch
          make || return 1
          make docs

          #install everything
          make INSTALL_ROOT=$pkgdir install

          install -D -m644 tools/assistant/tools/assistant/images/assistant.png ${pkgdir}/opt/qt-45/share/pixmaps/assistant.png
          install -D -m644 tools/linguist/linguist/images/appicon.png ${pkgdir}/opt/qt-45/share/pixmaps/linguist.png
          install -D -m644 tools/designer/src/designer/images/designer.png ${pkgdir}/opt/qt-45/share/pixmaps/designer.png
          install -D -m644 src/gui/dialogs/images/qtlogo-64.png ${pkgdir}/opt/qt-45/share/pixmaps/qtlogo.png
          install -d ${pkgdir}/opt/qt-45/share/applications
          install -m644 ${srcdir}/{linguist,designer,assistant,qtconfig}.desktop ${pkgdir}/opt/qt-45/share/applications/
          install -D -m644 LGPL_EXCEPTION.txt ${pkgdir}/opt/qt-45/share/licenses/qt/LGPL_EXCEPTION.txt

          install -d ${pkgdir}/opt/qt-45/share/doc
          cp -r doc/src/* ${pkgdir}/opt/qt-45/share/doc

          # cleanup and path fixes
          find $pkgdir/opt/qt-45/lib -type f -name '*prl' -print -exec sed -i -e "/^QMAKE_PRL_BUILD_DIR/d" {} \;
          sed -i -e "s|-L$srcdir/${_pkgfqn}/lib||g" $pkgdir/opt/qt-45/lib/pkgconfig/*.pc
          sed -i -e "s|$srcdir/${_pkgfqn}/bin/moc|/bin/moc|g" $pkgdir/opt/qt-45/lib/pkgconfig/*.pc
          sed -i -e "s|$srcdir/${_pkgfqn}/bin/uic|/bin/uic|g" $pkgdir/opt/qt-45/lib/pkgconfig/*.pc
}
